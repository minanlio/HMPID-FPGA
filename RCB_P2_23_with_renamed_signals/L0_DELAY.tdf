--  
-- MODULO PER TRATTARE IL SEGNALE L0_LVDS PROVENIENTE DA LTU: 
-- SI FA IN MODO CHE IL SEGNALE L0 GENERATO ABBIA UNA DURATA DI ALMENO 100 NSEC
--

SUBDESIGN L0_DELAY
(
	-- INPUT	
		CLOCK: INPUT; -- 160 MHz
		RESETn: INPUT;
		-- LTU
		L0_LVDS: INPUT;
		
	-- OUTPUT	
		-- L0_TO_COLUMN_GEN
		L0_LONG: OUTPUT; 
)

variable

-----------------------------------------------------------
-- REGISTERS
-----------------------------------------------------------
COUNTER[4..0]: DFFE;

-----------------------------------------------------------
-- NODES
-----------------------------------------------------------
ENA_COUNTER: NODE;
CLRN_COUNTER: NODE;

------------------------------------------------------------
-- STATE MACHINES
------------------------------------------------------------
DELAY_SM :	MACHINE WITH STATES (

				IDLE,
				L0_OUT,
				END_STATE
			);
				
				
BEGIN		

DEFAULTS

ENA_COUNTER = GND;
CLRN_COUNTER = VCC;
L0_LONG = GND;	
	
END DEFAULTS;


COUNTER[4..0].d = COUNTER[4..0] + 1;
COUNTER[4..0].clk = !CLOCK;
COUNTER[4..0].ena = ENA_COUNTER;
COUNTER[4..0].clrn = RESETn AND CLRN_COUNTER;

DELAY_SM.clk = CLOCK; 
DELAY_SM.reset =  !RESETn;


------------------------------------------------
------------------------------------------------
-- DELAY STATE MACHINE
------------------------------------------------
------------------------------------------------

CASE DELAY_SM is 

	WHEN IDLE => 
		ENA_COUNTER = GND;
		CLRN_COUNTER = VCC;
		L0_LONG = GND;
		IF L0_LVDS THEN  
			CLRN_COUNTER = GND;
			DELAY_SM = L0_OUT ; 
		ELSE
			DELAY_SM = IDLE; 
		END IF;
			
	WHEN L0_OUT =>
		ENA_COUNTER = VCC;
		CLRN_COUNTER = VCC;
		L0_LONG = VCC;
		IF COUNTER[4..0] == B"10000" THEN -- 1/160 MHz * 16 = 100 ns
			DELAY_SM = END_STATE;
		ELSE
			DELAY_SM = L0_OUT;
		END IF; 
			
	WHEN END_STATE =>
		ENA_COUNTER = GND;
		CLRN_COUNTER = GND;
		L0_LONG = GND;
		DELAY_SM = IDLE;
		
	WHEN OTHERS =>
		DELAY_SM = IDLE;
		
	END CASE;

END;
